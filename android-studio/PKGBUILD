# Maintainer: Philipp 'TamCore' B. <philipp [at] tamcore [dot] eu>

pkgname=android-studio
pkgver=0.2
pkgrel=1
_build=130.687321
pkgdesc="A new Android development environment based on IntelliJ IDEA."
arch=(i686 x86_64)
url="http://developer.android.com/sdk/installing/studio.html"
license=('APACHE')
depends=('python' 'fontconfig' 'mesa' 'libxrender' 'android-sdk')
makedepends=('unzip')
optdepends=('android-sdk-platform-tools' 'android-sdk-build-tools')
options=('!strip')
install=${pkgname}.install
source=(http://dl.google.com/android/studio/android-studio-bundle-${_build}-linux.tgz
        ${pkgname}.desktop
        vmoptions.patch)
md5sums=('6796d66de07c85b2822ca8d501a043c0'
         '850356f8cedad784f1f79e9cfaa38651'
         'be1810573bd3aaadabd90d14a642982c')

[ $CARCH = "x86_64" ] && depends=('python' 'lib32-fontconfig' 'lib32-mesa' 'lib32-libxrender' 'android-sdk')

prepare() {
    cd "${srcdir}/${pkgname}"

    # remove pause after open jdk warning
    sed -i 's/read IGNORE//' bin/studio.sh

    # extract the application icon
    unzip -o lib/resources.jar artwork/icon_AS_128.png

    # enable anti aliasing
    cd bin
    patch -p1 -i "${srcdir}/vmoptions.patch"
}

package() {
    cd "${srcdir}/${pkgname}"

    # application stuff
    mkdir -p "${pkgdir}/opt/${pkgname}"
    cp -a bin lib plugins "${pkgdir}/opt/${pkgname}"
    chmod -R 755 "${pkgdir}/opt/${pkgname}"

    mkdir -p "${pkgdir}/usr/bin"
    ln -s /opt/android-studio/bin/studio.sh "${pkgdir}/usr/bin/android-studio"

    # starter stuff
    install -Dm655 artwork/icon_AS_128.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
    install -Dm655 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
