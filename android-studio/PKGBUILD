# Maintainer: Philipp 'TamCore' B. <philipp [at] tamcore [dot] eu>

pkgname=android-studio
pkgver=0.1
pkgrel=6
_build=130.677228
pkgdesc="A new Android development environment based on IntelliJ IDEA."
arch=(i686 x86_64)
url="http://developer.android.com/sdk/installing/studio.html"
license=('APACHE')
depends=('python' 'fontconfig' 'mesa' 'libxrender' 'android-sdk')
makedepends=('unzip')
optdepends=('android-sdk-platform-tools' 'android-sdk-build-tools')
options=('!strip')
install=${pkgname}.install
source=(http://dl.google.com/android/studio/android-studio-bundle-${_build}-linux.tgz
        ${pkgname}.desktop)
md5sums=('62b9ce75e4b74b7c1236ea2f1f99da34'
         '8a429621df3ffc4e5aa4097fad757656')

[ $CARCH = "x86_64" ] && depends=('python' 'lib32-fontconfig' 'lib32-mesa' 'lib32-libxrender' 'android-sdk')

prepare() {
    cd "${srcdir}/${pkgname}"

    # remove pause after open jdk warning
    sed -i 's/read IGNORE//' bin/studio.sh

    # extract the application icon
    unzip -o lib/resources.jar artwork/icon_AS_128.png
}

package() {
    cd "${srcdir}/${pkgname}"

    # profile.d stuff
    mkdir -p "${pkgdir}/etc/profile.d"
    echo 'export PATH=$PATH:/opt/android-studio/bin' > "${pkgdir}/etc/profile.d/${pkgname}.sh"
    echo 'setenv PATH ${PATH}:/opt/android-studio/bin' > "${pkgdir}/etc/profile.d/${pkgname}.csh"
    chmod 755 "${pkgdir}/etc/profile.d/"*

    # application stuff
    mkdir -p "${pkgdir}/opt/${pkgname}"
    cp -a bin lib plugins "${pkgdir}/opt/${pkgname}"
    chmod -R 755 "${pkgdir}/opt/${pkgname}"
    ln -s /opt/android-studio/bin/studio.sh "${pkgdir}/opt/${pkgname}/bin/android-studio"

    # starter stuff
    install -Dm655 artwork/icon_AS_128.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
    install -Dm655 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
