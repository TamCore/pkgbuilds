# Maintainer: Philipp 'TamCore' B. <philipp [at] tamcore [dot] eu>
# Contributor: Paul Nicholson <brenix@gmail.com>
# Contributor: Nick B <Shirakawasuna at gmail _dot_com>
# Contributor: Alex Anthony <alex.anthony28991@googlemail.com>
# Based on coherence-svn by Jon Kristian Nilsen <jokr.nilsen@gmail.com>
##########################################################
_use_dbus="n" # Creates dbus service and uses ~/.coherence for config. Otherwise it will use an init script and /etc/coherence.conf
_samsung_patch="n" # patches fsstore backend for compatibility with Samsung TVs
##########################################################

pkgname=coherence
pkgver=0.6.6.2
pkgrel=3
pkgdesc="A DLNA/UPnP MediaServer and MediaRenderer"
arch=('i686' 'x86_64')
url="http://coherence.beebits.net/"
license=('MIT')
depends=('python2' 'epsilon' 'axiom' 'twisted-web2' 'python-elementtree' 'python2-configobj' 'pylouie' 'libcaca')
optdepends=('ampache: for the Ampache Backend'
            'elisa: for the Elisa DB Backend and Elisa DLNA Player'
            'gstreamer0.10-python: for the gstreamer MediaRenderer'
            'libmtag: for MediaStore Backend ID3 Option A'
            'pyid3lib: for MediaStore Backend ID3 Option B'
            'python-gdata: for the Picasa and YouTube Backends'
            'python-mechanize: for the Flickr Backend'
            'rhythmbox: for the Rhythmbox DB Backend'
            'taglib: for MediaStore Backend ID3 Option A or C'
            'tagpy: for MediaStore Backend ID3 Option C'
            'tracker: for the TrackerStore Backend'
            'dvb-daemon: for the DVB Daemon Backend (no package yet)')
makedepends=('python2-distribute')
conflicts=('coherence-svn')
source=(http://coherence.beebits.net/download/Coherence-${pkgver}.tar.gz
       coherence.rc
       coherence.conf
       coherence.service
       coherence_pidfile.patch
       samsung.patch
       org.Coherence.service)
backup=('etc/coherence.conf')
sha256sums=('0b54a6ba88c1ff6274aadb68ff37b8a3961b4c6acf5ded291c5dea936a311ba4'
            '29619e954d6747b78c7c8b1d15188e9a623e3052040b9f5e814adde17859efd6'
            '89c1f53dea466f941dc1ee74d62900134c27e840c2a81e3d46b6c8cb0f8ee617'
            '0dee74b2777ffa60bc912ecb3756d5fbc2ed32c4866d633b1cc9c0edf03ce5ea'
            'fb11014e27ee5dd4fc70145212e85c926e392e43afe10ebff565db260cfe0464'
            '4b242a70452e9b394c69e440a620016a4e2c86ffe772ce112094b04d97e2eb0a'
            '732a74062dfb3f712432a81bd8b7dac2117281f8043a467e23b80834e1252a56')

prepare() {
  cd "${srcdir}/Coherence-${pkgver}"

  if [ ${_samsung_patch} = "y" ]; then
    msg "Patching source with Samsung Patch"
    patch -Np0 < ${srcdir}/samsung.patch
  fi

  patch -p1 < "${srcdir}/coherence_pidfile.patch"
}

package() {
  cd "${srcdir}/Coherence-${pkgver}"

  python2 setup.py install --prefix=/usr --root="${pkgdir}"

  #dbus fix
  if [ ${_use_dbus} = "y" ]; then
    install -D -m644 "${srcdir}/org.Coherence.service" "${pkgdir}/usr/share/dbus-1/services/org.Coherence.service"
  else
    install -D -m755 "${srcdir}/coherence.conf" "${pkgdir}/etc/coherence.conf"
    install -D -m755 "${srcdir}/coherence.rc" "${pkgdir}/etc/rc.d/coherence"
  fi
  install -D -m644 "${srcdir}/Coherence-${pkgver}/LICENCE" "${pkgdir}/usr/share/licenses/coherence/LICENSE"
}
