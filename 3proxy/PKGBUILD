pkgname=3proxy
pkgver=0.7.1.1
pkgrel=1
pkgdesc="A tiny crossplatform proxy server"
arch=('i686' 'x86_64')
url="http://www.3proxy.ru/"
license=('custom')
source=("http://www.3proxy.ru/$pkgver/3proxy-$pkgver.tgz")
md5sums=('cbc22bf28644c5f3092aa1d925de7d33')

prepare() {
	cd "$srcdir/$pkgname"
	sed -i 's/^INSTALL_CFG_OBJS2.*/INSTALL_CFG_OBJS2 =/' Makefile.Linux
}
build() {
	cd "$srcdir/$pkgname"
	make -f Makefile.Linux
}
package() {
	cd "$srcdir/$pkgname"
	make -f Makefile.Linux DESTDIR="$pkgdir/" prefix="/usr" ETCDIR="$pkgdir/etc/$pkgname" install
	mv "$pkgdir/usr/bin/proxy" "$pkgdir/usr/bin/3proxy"
	install -D -m644 "$srcdir/$pkgname/cfg/3proxy.cfg.sample" "$pkgdir/etc/$pkgname/3proxy.cfg.sample"
	install -D -m644 "$srcdir/$pkgname/cfg/counters.sample" "$pkgdir/etc/$pkgname/counters.sample"
	install -D -m644 copying "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
